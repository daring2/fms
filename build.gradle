plugins {
    id 'java'
    id 'maven'
    id "org.jetbrains.kotlin.jvm" version "1.1.3-2"
    id 'com.github.ben-manes.versions' version '0.15.0'
}

ext {
    sl4jVersion = '1.7.25'
    kotlinVersion = '1.1.3-2'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'kotlin'

    group = 'com.gitlab.daring.monitoring'
    buildDir = 'target'
    archivesBaseName = 'mon-' + archivesBaseName

    compileJava.options.encoding = 'UTF-8'

    compileKotlin {
        kotlinOptions.jvmTarget = "1.8"
    }

    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        compile "org.jetbrains.kotlin:kotlin-stdlib-jre8:$kotlinVersion"
        compile "org.slf4j:slf4j-api:$sl4jVersion"
        compile "ch.qos.logback:logback-classic:1.2.3"
        testCompile "junit:junit:4.12"
        testCompile "org.mockito:mockito-core:2.7.12"
    }

    task copyDeps(type: Sync, dependsOn: build) {
        from(configurations.runtime, jar)
        into "$buildDir/dependency"
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier = 'sources'
        from sourceSets.main.allSource
    }

    artifacts {
        archives sourcesJar
    }

}

task wrapper(type: Wrapper) {
    gradleVersion = '3.5'
}

apply from: 'private.gradle'